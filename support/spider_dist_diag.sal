% -----------------------------------------------------------
% Lee Pike
% leepike @ galois.com
%
% SAL 2.4
% -----------------------------------------------------------
spider_dist_diag: CONTEXT =
BEGIN

REALTIME            : TYPE = REAL;
CLOCKTIME           : TYPE = INTEGER;
OFFSET              : TYPE = {T: CLOCKTIME | T >= 0};
RND                 : TYPE = NATURAL; 
rho                 : REALTIME = 1/10000; 
d_nom               : {t: REALTIME | t >= 0} = 5; 
ERROR               : TYPE = {t: REALTIME |     t >= 0 
                                            AND t < d_nom};
e_l                 : ERROR = 5/10000; 
e_u                 : ERROR = 5/10000; 
% floor((1 - rho) * (d_nom - e_l))
fl_d_min            : CLOCKTIME = 4;
% ceiling((1 + rho) * (d_nom + e_u))
ce_d_max            : CLOCKTIME = 6;

independent?(r: RND): BOOLEAN = FALSE; 

% -- constraints on time-triggered systems ------------------
constraint1(P: OFFSET, pre_sched: CLOCKTIME, 
            sched: CLOCKTIME): BOOLEAN = 
  0 < P AND P < sched - pre_sched;

constraint2(D: CLOCKTIME, S: OFFSET): BOOLEAN = 
  D >= S - fl_d_min;

constraint3(P: OFFSET, D: CLOCKTIME, S: OFFSET): BOOLEAN = 
  P > D + S + ce_d_max;

constraint4(r: RND, D: CLOCKTIME): BOOLEAN = 
  (NOT independent?(r)) => D >= 0;

constraint5(pre_P: OFFSET, D: CLOCKTIME, pre_sched: CLOCKTIME, 
            sched: CLOCKTIME): BOOLEAN =
  D >= pre_P - sched + pre_sched;

constraint6(D: CLOCKTIME, R: CLOCKTIME, S: OFFSET): BOOLEAN = 
  R - 1 <= D + fl_d_min - S;
% -----------------------------------------------------------

SYSTEM: MODULE =
BEGIN
LOCAL
  r                      : RND,
  sched, D               : CLOCKTIME, 
  P, S, R                : OFFSET,    
  ind                    : BOOLEAN,   
  c1, c2, c3, c4, c5, c6 : BOOLEAN
INITIALIZATION
  r = 0;
  sched = 2;
  D = 1;
  P = 13; 
  S = 4; 
  R = 2;

  c1 = TRUE; 
  c2 = constraint2(D, S);
  c3 = constraint3(P, D, S);
  c4 = constraint4(r, D);
  c5 = TRUE;
  c6 = constraint6(D, R, S) 
TRANSITION
[
    TRUE
    -->
      r' = IF r = 3 THEN 0 ELSE r + 1 ENDIF;
      sched' = sched+14;

      c1' = constraint1(P', sched, sched');
      c2' = constraint2(D', S');
      c3' = constraint3(P', D', S');
      c4' = constraint4(r', D');
      c5' = constraint5(P, D', sched, sched');
      c6' = constraint6(D', R', S')
]
END;

% sal-inf-bmc -i -d 2 diag_sched.sal constraints
constraints: LEMMA SYSTEM |- G(    c1 AND c2 AND c3 
                               AND c4 AND c5 AND c6);

% For k >= 0, the following should be false:
% sal-inf-bmc -i -d k diag_sched.sal constraints 
liveness: LEMMA SYSTEM |- G(r <= 2);
END
